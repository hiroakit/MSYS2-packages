
_realname=byacc
pkgname=btyacc
pkgver=20191125
pkgrel=1
pkgdesc="btyacc - an LALR(1) parser generator with support for backtracking"
arch=('i686' 'x86_64')
url="https://invisible-island.net/byacc"
license=('Public Domain')
groups=('base-devel')

source=("${_realname}-${pkgver}.tar.gz::https://invisible-island.net/datafiles/release/${_realname}.tar.gz")
sha256sums=('071c2ebe36afaa8448b80e893473a681e63a3b8a4ed636c0d675780a02411cde')


prepare() {
  cd ${_realname}-${pkgver}
  autoreconf -vfi
}

build() {
  cd ${_realname}-${pkgver}
  mkdir -p build-${MSYSTEM_CHOST}
  pushd build-${MSYSTEM_CHOST}
  ../configure -C \
    --enable-btyacc \
    --program-prefix=bt \
    --with-max-table-size=18000 \
    --program-transform-name='s,\^,bt,'
  make
}

check() {
  cd ${_realname}-${pkgver}/build-${MSYSTEM_CHOST}
  make check
}

package() {
  cd ${_realname}-${pkgver}
  mkdir -p "${pkgdir}/usr/share/licenses/${pkgname}"
  cp -pv package/debian/copyright "${pkgdir}/usr/share/licenses/${pkgname}"
  cd build-${MSYSTEM_CHOST}
  make DESTDIR=${pkgdir} install
}
