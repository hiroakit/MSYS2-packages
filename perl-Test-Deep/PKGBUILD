# Maintainer: Peter Budai <peterbudai@hotmail.com>

_realname=Test-Deep
pkgname=perl-${_realname}
pkgver=1.130
pkgrel=1
pkgdesc="Extremely flexible deep comparison"
arch=('any')
url="https://metacpan.org/release/Test-Deep"
license=('GPL' 'PerlArtistic')
depends=('perl' 'perl-Test-Simple' 'perl-Test-NoWarnings')
options=('!emptydirs')
groups=('perl-modules')
source=("https://cpan.metacpan.org/authors/id/R/RJ/RJBS/${_realname}-${pkgver}.tar.gz")
sha256sums=('4064f494f5f62587d0ae501ca439105821ee5846c687dc6503233f55300a7c56')

build() {
  cd ${_realname}-${pkgver}
  perl Makefile.PL installdirs=vendor
  make
}

check() {
  cd ${_realname}-${pkgver}
  make test
}

package() {
  cd ${_realname}-${pkgver}
  make DESTDIR="${pkgdir}" install
}
