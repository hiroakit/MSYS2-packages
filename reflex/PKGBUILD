
_realname=reflex
pkgname=${_realname}
pkgver=20191123
pkgrel=1
pkgdesc="A variant of the flex fast lexical scanner"
arch=('i686' 'x86_64')
url="https://invisible-island.net/reflex"
license=('BSD')
groups=('base-devel')

source=("${_realname}-${pkgver}.tar.gz::https://invisible-island.net/datafiles/release/${_realname}.tar.gz")
sha256sums=('5623f4a520fa2f51abb23037fbb6b2aafac0e9cc60ddaa70ba727e0e01ef053f')


prepare() {
  cd ${_realname}-${pkgver}
  autoreconf -vfi
}

build() {
  cd ${_realname}-${pkgver}
  mkdir -p build-${MSYSTEM_CHOST}
  pushd build-${MSYSTEM_CHOST}
  ../configure -C
  make
}

check() {
  cd ${_realname}-${pkgver}/build-${MSYSTEM_CHOST}
  make check
}

package() {
  cd ${_realname}-${pkgver}
  mkdir -p "${pkgdir}/usr/share/licenses/${pkgname}"
  cp -pv package/debian/copyright "${pkgdir}/usr/share/licenses/${pkgname}"
  cd build-${MSYSTEM_CHOST}
  make DESTDIR=${pkgdir} install
}
