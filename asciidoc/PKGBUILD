# Maintainer: Alexey Pavlov <alexpux@gmail.com>

pkgname=asciidoc
_basever=9.0.0
_subver=rc1
pkgver=${_basever}${_subver}
pkgrel=1
pkgdesc='Text document format for short documents, articles, books and UNIX man pages.'
arch=('any')
url='https://asciidoc.org/'
license=('GPL')
depends=('python' 'libxslt' 'docbook-xsl')
makedepends=('tar')
groups=('base-devel')
# optdepends=('lilypond: music-filter'
            # 'imagemagick: music-filter (used in conjunction with lilypond)'
            # 'source-highlight: source-highlight-filter'
            # 'dblatex: pdf generation'
            # 'fop: alternative pdf generation'
            # 'lynx: text generation'
            # 'w3m: text generation (alternative to lynx)')
source=("${pkgname}-${pkgver}.tar.gz::https://github.com/asciidoc/asciidoc-py3/archive/${pkgver}.tar.gz"
        0001-W32-confdir.msys2.patch)
noextract=("${pkgname}-${pkgver}.tar.gz")
sha256sums=('8d5a22039943e43374eedaa1ea3e17a79bf29dd671918c090f535b1b3c6c0997'
            '71f44b513f627b133afead38576fcb8a5460d0514174a4ce219c6ea761d8b40c')

prepare() {
  tar -xzvf ${pkgname}-${pkgver}.tar.gz --recursive-unlink

  cd ${pkgname}-py3-${pkgver}

  patch -p1 -i ${srcdir}/0001-W32-confdir.msys2.patch
}

build() {
  cd ${pkgname}-py3-${pkgver}

  autoreconf -fi

  ./configure \
    --prefix=/usr \
    --sysconfdir=/etc

  make
}

package() {
  cd ${pkgname}-py3-${pkgver}

  make install DESTDIR=${pkgdir}

  install -Dm644 asciidocapi.py \
    ${pkgdir}/usr/lib/python3.7/site-packages/asciidocapi.py

  install -Dm644 vim/syntax/asciidoc.vim \
    ${pkgdir}/usr/share/vim/vimfiles/syntax/asciidoc.vim
}
